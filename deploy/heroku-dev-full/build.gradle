////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Project deploy heroku-dev-full
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

import org.apache.tools.ant.taskdefs.condition.Os
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'java'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'io.freefair.lombok'
  id 'jacoco'

  id 'com.google.cloud.tools.jib'
}

dependencyManagement {
  imports {
    mavenBom SpringBootPlugin.BOM_COORDINATES
  }
}

dependencies {
  implementation project(':common')
  implementation project(':legacy')

  implementation 'org.postgresql:postgresql'
  implementation 'com.h2database:h2' // for local development
  implementation 'com.zaxxer:HikariCP'

  testImplementation project(':common')

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: "com.vaadin.external.google", module: "android-json"
  }
  testImplementation 'org.springframework.boot:spring-boot-starter-data-rest'
  testImplementation 'org.springframework.security:spring-security-test'

  testImplementation 'org.hibernate:hibernate-core'
  testImplementation 'com.fasterxml.jackson.core:jackson-databind'
  testImplementation 'com.querydsl:querydsl-jpa'

  testImplementation "org.assertj:assertj-core:${assertj_core_version}"
  testImplementation "org.json:json:${json_version}"
  testImplementation 'com.unboundid:unboundid-ldapsdk'
  testImplementation 'org.springframework.ldap:spring-ldap-core'
  testImplementation 'org.springframework.boot:spring-boot-starter-security'
  testImplementation 'org.springframework.security:spring-security-ldap'
}

jacocoTestReport {
  reports {
    xml.required.set(true)
  }
}

test.finalizedBy jacocoTestReport

jib {
  from {
    if (Os.isArch("aarch64")) {
      image = 'arm64v8/amazoncorretto:11'
    } else {
      image = 'amazoncorretto:11'
    }
  }
  to {
    image = "${name}:latest"
  }
  container {
    mainClass = 'org.sitmun.Application'
  }
}

springBoot {
  getMainClass().set("org.sitmun.Application")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
